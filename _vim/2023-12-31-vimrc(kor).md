---
layout: single
title: "vim강의-01-한국 프로그래밍을 위한 Vim 최적화: .vimrc에 대한 심층 분석"
search: true
excerpt: "한국 프로그래머를 위해 맞춤 설정된 .vimrc. Vim 코딩을 쉽게 만드는 조정과 플러그인을 알려줍니다."
categories:
- vim 
last_modified_at: 2023-12-31
permalink: /vim/explain-vimrc-kor/
---

## 소개

Vim 최적화에 관한 심층 가이드에 오신 것을 환영합니다. 효율성과 맞춤 설정으로 알려진 Vim은 올바르게 조정되었을 때 강력한 도구가 될 수 있습니다. 이 게시물에서는 한국어와 같은 다국어 환경에서 코딩 경험을 향상시키는 `.vimrc` 구성을 설명하고 각 맞춤 설정의 이유와 그것이 어떻게 코딩 경험을 향상시키는지를 설명하겠습니다.

## `.vimrc` 파일 이해하기

### 다중 바이트 문자 지원 및 홈 디렉토리

```vim
"multi byte char
scriptencoding=utf8

"vim HOME
let $VIMDIR = $HOME . '/.vim'
```

이 줄에서는 UTF-8로 스크립트 인코딩을 설정하여 Vim이 한글 스크립트와 같은 다중 바이트 문자를 처리할 수 있도록 합니다. `$VIMDIR` 변수는 Vim 구성과 플러그인을 위한 전용 디렉토리를 설정하여 모든 것을 정돈된 상태로 유지합니다.

### 구문 및 클립보드 설정

```vim
"settings
syntax on "구문 강조를 활성화 합니다.
"+레지스터가 사용 가능한 경우 'unnamedplus'를 사용하고, 그렇지 않으면 'unnamed'를 사용합니다.
let &clipboard = has('unnamedplus') ? 'unnamedplus' : 'unnamed'
```

가독성을 위해 구문 강조는 필수입니다. 클립보드 구성은 특히 Vim과 다른 프로그램 간의 복사 및 붙여넣기를 원활하게 하는 '+' 레지스터를 사용할 수 있으면 사용합니다.

### 시각적 요소 사용자 정의

```vim
"========== 다양한 옵션 선택 ==========  
se number "line number를 표시합니다.
... (이하 생략)
```

여기에서는 줄 번호부터 색상 스킴에 이르기까지 시각적 요소를 사용자 정의하는 방법에 대해 설명합니다. 

### 검색 및 실행 취소 기능 향상

```vim
"========== 히스토리 및 실행 취소 설정 =========  
... (이하 생략)
```

점진적이고 대소문자를 구별하는 스마트 검색으로 코드 탐색을 쉽게 만드는 방법에 대해 자세히 설명합니다. 실행 취소 설정은 원활한 코딩 경험을 위해 특히 중요합니다.

### 플러그인 관리 및 사용자 정의 키

```vim
"plugin manager
... (이하 생략)
```

Vim에서 플러그인을 효율적으로 관리하는 것이 중요합니다.

저는 `vim-plug`를 사용하고 `fzf`, `coc.nvim`, `vim-snippets`과 같은 필수 플러그인 목록을 가지고 있습니다. 또한 작업 속도를 높이는 사용자 정의 키 바인딩에 대해서도 논의합니다.

---
아래는 제가 설정한 vimrc와 자세한 설명입니다.   
여기에 링크가 있습니다.   
[github .vimrc 코드](https://github.com/ThinkAllofYours/LandR_dotfile/blob/main/.vimrc)
```
"multi byte char
scriptencoding=utf8

"vim HOME
let $VIMDIR = $HOME . '/.vim'

"start settings

"settings
syntax on "구문 강조를 활성화 합니다.
"+레지스터가 사용 가능한 경우 'unnamedplus'를 사용하고, 그렇지 않으면 'unnamed'를 사용합니다.
let &clipboard = has('unnamedplus') ? 'unnamedplus' : 'unnamed'

"========== 다양한 옵션 선택 ==========
se number "line number를 표시합니다. ex) 왼쪽에 보이는 숫자표시
se nowrap "줄 바꿈을 비활성화.
se nobackup "백업 파일을 유지.
se noswapfile "스왑 파일 생성을 비활성화.
se backspace=2 "삽입 모드(i)에서 backspace를 사용 가능
se bg=dark "어두운 배경을 사용.
se signcolumn=yes "오류 표시같은걸 표시할 수 있도록 허용
se incsearch "부분 일치를 보여주는 증분 검색을 활성화.
se hlsearch "검색결과 강조 표시."
se smartcase "패턴이 전부 소문자인 경우 검색 패턴에서 대소문자를 표시
se ignorecase "검색 패턴에서 대소문자를 무시
se list "보이지 않는 문자를 표시.
se listchars=tab:.\ ,trail:•,nbsp:. "보이지 않는 문자가 표시되는 방식
"========== 탭 및 들여쓰기 설정 ==========
se autoindent "새줄에서 현재 줄의 들여쓰기 계속 이어서 사용
se shiftwidth=2 "들여쓰기 각 단계에 사용되는 공백수
se tabstop=2 "탭이 대신하는 공백수
se softtabstop=2 "공백을 마치 탭이 있는 것처럼 사용 예를 들어 2개의 스페이스면 하나의 탭으로 인식
se expandtab "탭을 공백으로 변환
se smarttab "줄의 시작 부분분에서 위shiftwidth를 기준으로 공백삽입
"========== 히스토리 및 실행 취소 설정 =========
se history=1000 "명렁어 히스토리의 줄수
se undofile "각 파일에 대해 실행 취소 파일유지
se undolevels=1000 "실행취소 최소 수준
se undoreload=10000 "버퍼를 다시 로드할 때 실행 취소에 저장되는 줄수
se undodir=$VIMDIR/undo// "되돌리기 파일을 저장할 디렉토리를 설정 경로끝의 '//'는 각 파일마다 고유한 되돌리기 파일을 가짐
"========== text 보기 설정 =========
se colorcolumn=99 "99번재 문자에 시각적 열가이드를 설정
se synmaxcol=500 "한 줄의 처음 500문자까지만 구문처리(넘어가면 다음줄)
se fileencodings=utf-8,cp949,euc-kr,euc-jp,latin1 "파일을 열 떄 시도하는 문자 인코딩 순서
se virtualedit=onemore "현재 줄끝 계속 문자를 추가할 수 있도록 설정(기본 vim에서는 문자를 기준으로 이동)
se pastetoggle=<F12> "F12키로 붙여넣기 모드를 전환
se autoread "외부에서 파일이 변경되었을때 업데이트
se hidden "저장하지 않고 버퍼간에 전환가능
se termguicolors "다양한 생상 지원
se laststatus=2 "항상 상태를 표시
se updatetime=200 "자동 명령전 Vim이 대기하는 시간 설정
se shortmess+=mcF "특정 메시지를 축소(수정중,명령어,파일경로)
se ttimeout ttimeoutlen=100 "키 코드 시간 초과 제어
"========== other options =========
se nowritebackup "파일 저장전에 기존파일 백업복사본을 만들지 않는다.
se noshowcmd "현재 입력중인 명령어를 보여주지 않도록 설정
se lazyredraw "vim이 명령어를 실행하는 동안 화면을 다시 그리지 않는다.
se scrolloff=3 "화면을 스크롤할 때 커서를 화면 상단과 하단에서 최소 3줄 떨어진 위치에 유지.
se path+=** "Vim이 현재 디렉토리와 모든 하위 디렉토리를 재귀적으로 검색
se mouse= "마우스 동작구성 비활성화

"custom keys
"nnor 명령어를 사용하여 일반 모드(normal mode)에서의 키 바인딩을 설정
"ctrl + L 현재화면에서 하이라이트를 제거합니다.  Vim이 diff 모드에서 실행 중일 때 diffupdate 명령을 실행
"명령들이 실행된 후 화면을 갱신(다양한 작업을 한 번의 키 입력으로 실행)
nnor <silent> <C-L> :noh<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>

"cursor restore
"파일을 다시 열 때 마지막으로 편집한 위치로 커서를 자동으로 이동시키는 기능
au BufReadPost * if line("'\"") > 1 &&
    \ line("'\"") <= line("$") | execute "normal! g`\"" | en

"-------------------------------------------------------------------------------
"   plugins minimum!!!
"===============================================================================

"plugin manager
"플러그인의 디렉토리를 설정합니다. $VIMDIR은 Vim의 설정과 플러그인이 저장된 위치를 가리키는 환경변수
call plug#begin($VIMDIR . '/plugged')

"커맨드 라인 퍼지 파인더(경로내 파일 찾기, 명령줄 찾기)
Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
let g:fzf_preview_window = '' "FZF에서 미리보기 창을 비활성화
"순서대로 명령어 검색, 파일 검색, 실버서치(파일내용검색)
nnor <silent> <space>r :History<CR>
nnor <silent> <space>t :Files<CR>
nnor <silent> <space>a :Ag <C-R><C-W><CR>

"미리 정의된 코드 구조(스닙펫)
Plug 'honza/vim-snippets'

"-------------------------------------------------------------------------------
"   coc.nvim
"===============================================================================

"coc.nvim 플러그인 설치(자동완성도구)
"본인언어에서 추가로 필요한건 여기에 추가하면 됨
Plug 'neoclide/coc.nvim', {'do': 'yarn install --frozen-lockfile'}
let g:coc_global_extensions = [
            \ 'coc-snippets',
            \ 'coc-prettier',
            \ 'coc-vimlsp',
            \ 'coc-sh',
            \ 'coc-html',
            \ 'coc-htmlhint',
            \ 'coc-eslint',
            \ 'coc-htmldjango',
            \ 'coc-css',
            \ 'coc-cssmodules',
            \ 'coc-xml',
            \ 'coc-docker',
            \ 'coc-solargraph',
            \ 'coc-markdownlint',
            \ 'coc-tsserver',
            \ 'coc-html-css-support',
            \ 'coc-sql',
            \ 'coc-pyright',
            \ 'coc-json',
            \ 'coc-git',
            \ 'coc-yaml',
            \ 'coc-pairs',
            \ 'coc-diagnostic',
            \ '@yaegassy/coc-nginx',
            \ ]

"코드내에 변수, 함수등에 따라 다양한 칼라로 표시
Plug 'nvim-treesitter/nvim-treesitter', {'do': ':TSUpdate'}

"주석생성도우미
Plug 'kkoomen/vim-doge', { 'do': 'npm i --no-save && npm run build:binary:unix' }

"https://github.com/nvim-treesitter/nvim-treesitter/wiki/Colorschemes
"vim 테마
Plug 'sainnhe/sonokai'
let g:sonokai_style = 'shusia'
let g:sonokai_better_performance = 1

"coc
"Coc 동작을 선택할 수 있는 리스트 창
nnor <silent> <space>l :CocList<CR>
"코드 정렬
nnor <silent> <space>f :call CocAction('format')<CR>
"함수나 변수 정의와 같은 정보를 보여줌
nnor <silent> K :call CocAction('doHover')<CR>

"[c 와 ]c 는 각각 이전 및 다음 변경 청크로 이동하는 데 사용
nmap <silent> <expr> [c &diff ? '[c' : '<Plug>(coc-git-prevchunk)'
nmap <silent> <expr> ]c &diff ? ']c' : '<Plug>(coc-git-nextchunk)'
"<space>c 는 현재 Git 청크에 대한 정보를 표시합니다.
nmap <silent> <space>c <Plug>(coc-git-chunkinfo)

"오류와 경고와 같은 진단 메시지를 탐색
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

"gd는 해당 클래스의 정의로 이동(클래스,함수,변수등이 정의되어 있는 곳으로 이동)
nmap <silent> gd <Plug>(coc-definition)
"gy는 타입 정의로 이동
nmap <silent> gy <Plug>(coc-type-definition)
"gi는 구현으로 이동
nmap <silent> gi <Plug>(coc-implementation)
"gr은 모든 참조를 나열
nmap <silent> gr <Plug>(coc-references)

" enhanced CR
" 자동완성 팝업이 보일 때 enter키 동작 개선
inor <silent><expr> <CR> coc#pum#visible() ? "<CR>" : "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"

"coc-highlight
"지연된 커서 아래 심볼 자동 하이라이트
autocmd CursorHold * silent call CocActionAsync('highlight')

"coc-snippets
"백스페이스가 스니펫 확장을 트리거해야하는지 결정
"탭 키는 스니펫 탐색 및 확장을 위해 구성됨
func! CheckBackspace() abort
  let col = col('.') - 1

  return !col || getline('.')[col - 1]  =~# '\s'
endfunc

"완성 팝업이 보일 떄, 탭은 강조된 항목 선택
"스니펫이 확장 가능하거나 점프 가능할 때, 탭은 스니펫 동작을 트리거
"커서가 공백 뒤에 있거나 줄의 시작 부분에 있을 떄, 탭은 문자를 삽입
"그렇지 않으면 Coc에게 제안을 새로 고칠것
let g:coc_snippet_next = '<tab>'
inor <silent><expr> <Tab>
      \ coc#pum#visible() ? coc#_select_confirm() :
      \ coc#expandableOrJumpable() ? "\<C-r>=coc#rpc#request('doKeymap', ['snippets-expand-jump',''])\<CR>" :
      \ CheckBackspace() ? "\<Tab>" :
      \ coc#refresh()

```